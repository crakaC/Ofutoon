apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.crakac.ofutoon"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            def p = getProps("app/debug_token.properties")
            p.each { k, v ->
                def val = v.toString()
                if (val.startsWith("\"")) {
                    buildConfigField "String", k, v
                } else if (val.endsWith('f')) {
                    buildConfigField "float", k, v
                } else if (val.contains(".")) {
                    buildConfigField "double", k, v
                } else if (val.endsWith("L")) {
                    buildConfigField "long", k, v
                } else {
                    buildConfigField "int", k, v
                }
            }
        }
    }
    dataBinding {
        enabled = true
    }

    kotlinOptions {
        jvmTarget = 1.8
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

def getProps(String file) {
    def propFile = rootProject.file(file)
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    return props
}

dependencies {
    ext {
        appcompat_version = '1.2.0-alpha01'
        material_version = '1.2.0-alpha02'
        pallete_version = '1.0.0'
        room_version = '2.2.2'
        annotation_version = '1.1.0'
        exifinterface_version = '1.0.0'
        photoview_version = '2.3.0'
        glide_version = '4.10.0'
        retrofit_version = '2.7.0'
        okhttp_version = '4.2.2'
    }
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.palette:palette:$pallete_version"
    implementation "androidx.annotation:annotation:$annotation_version"
    implementation "androidx.exifinterface:exifinterface:1.1.0"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation "com.github.chrisbanes:PhotoView:$photoview_version"

    implementation("com.github.bumptech.glide:glide:$glide_version")
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    testImplementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
}
