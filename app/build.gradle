apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.crakac.ofutoon"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            def p = getProps("app/debug_token.properties")
            p.each { k, v ->
                def val = v.toString()
                if (val.startsWith("\"")) {
                    buildConfigField "String", k, v
                } else if (val.endsWith('f')) {
                    buildConfigField "float", k, v
                } else if (val.contains(".")) {
                    buildConfigField "double", k, v
                } else if (val.endsWith("L")) {
                    buildConfigField "long", k, v
                } else {
                    buildConfigField "int", k, v
                }
            }
        }
    }
    dataBinding {
        enabled = true
    }
}

def getProps(String file) {
    def propFile = rootProject.file(file)
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    return props
}

dependencies {
    ext {
        support_library_version = '28.0.0'
        photoview_version = '2.1.4'
        glide_version = '4.8.0'
        retrofit_version = '2.5.0'
        okhttp_version = '3.11.0'
        room_version = '1.1.1'
        dagger_version = '2.20'
    }
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:palette-v7:$support_library_version"
    implementation "com.android.support:support-annotations:$support_library_version"
    implementation "com.android.support:exifinterface:$support_library_version"

    implementation "android.arch.persistence.room:runtime:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"
    testImplementation "android.arch.persistence.room:testing:$room_version"

    implementation "com.github.chrisbanes:PhotoView:$photoview_version"

    implementation("com.github.bumptech.glide:glide:$glide_version")
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    testImplementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
